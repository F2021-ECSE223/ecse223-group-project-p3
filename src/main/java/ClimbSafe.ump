namespace ca.mcgill.ecse.climbsafe.model;
use ClimbSafeStates.ump;
use ClimbSafePersistence.ump;

class ClimbSafe {
  Date startDate;
  Integer nrWeeks;
  Integer priceOfGuidePerWeek;
  1 <@>- 0..1 Administrator administrator;
  1 <@>- * Guide guides;
  1 <@>- * Member members;
  1 <@>- * BookedItem bookedItems;
  1 <@>- * Equipment equipment;
  1 <@>- * EquipmentBundle bundles;
  1 <@>- * BundleItem bundleItems;
  1 <@>- * Hotel hotels;
  1 <@>- * Assignment assignments;
}

class User {
  abstract; 
  unique email;
  password;
}

class Administrator {
  isA User;
}

class NamedUser {
  abstract;
  isA User;
  name;
  emergencyContact;
}

class Guide {
  isA NamedUser;
}

class Member {
  isA NamedUser;
  Integer nrWeeks;
  Boolean guideRequired;
  Boolean hotelRequired;
}

associationClass BookedItem {
  Integer quantity;
  * Member member;
  * BookableItem item;
}

class BookableItem {
  abstract;
  unique name;
}

class Equipment {
  isA BookableItem;
  Integer weight;
  Integer pricePerWeek;
}

class EquipmentBundle {
  isA BookableItem;
  Integer discount;
}

associationClass BundleItem {
  Integer quantity;
  * EquipmentBundle bundle;
  * Equipment equipment;
}

class Hotel {
  unique name;
  address;
  enum HotelRating { OneStar, TwoStars, ThreeStars, FourStars, FiveStars }
  HotelRating rating;
} 

class Assignment {
  Integer startWeek;
  Integer endWeek;
  0..1 -- 1 Member member;
  * -- 0..1 Guide guide;
  * -- 0..1 Hotel hotel;
}//$?[End_of_model]$?

namespace -;


class ClimbSafe
{
  position 50 30 109 45;
  position.association BookedItem:bookedItems__ClimbSafe 186,93 0,10;
  position.association ClimbSafe__EquipmentBundle:bundles 31,93 30,0;
  position.association BundleItem:bundleItems__ClimbSafe 220,4 0,10;
  position.association ClimbSafe__Hotel:hotels 220,42 0,10;
  position.association Assignment:assignments__ClimbSafe 220,80 0,10;
}

class User
{
  position 50 130 109 45;
}

class Administrator
{
  position 50 230 109 45;
}

class NamedUser
{
  position 50 330 109 45;
}

class Guide
{
  position 50 430 109 45;
}

class Member
{
  position 250 30 109 45;
}

class BookedItem
{
  position 250 130 109 45;
}

class BookableItem
{
  position 250 230 109 45;
}

class Equipment
{
  position 250 330 109 45;
}

class EquipmentBundle
{
  position 250 430 109 45;
}

class BundleItem
{
  position 450 30 109 45;
}

class Hotel
{
  position 450 130 109 45;
}

class Assignment
{
  position 450 230 109 45;
  position.association Assignment__Guide:guide 0,10 110,10;
}